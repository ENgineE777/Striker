#import "Main"

class Soldier
{
    Mesh@ mesh;
    Phys3D@ phys;
    int visible = 0;
    bool spawned = false;
    int grab_state = 0;
    float time_to_move = 1.0f;
}

class Mission2 : Mission
{
    //1
    EnemyPool@ centers;
    array<Marker> commandersSpawn;
    array<Soldier> commanders;

    //2
    EnemyPool@ powerPlants;

    //3
    EnemyPool@ barracks;
    Marker spySpawn;
    Soldier spy;

    Main@ main;

    void OnInit()
    {
        main.RegisterMission(this);

        for (int i = 0; i<main.enemy_pools.get_length();i++)
        {
            auto@ pool = main.enemy_pools[i];

            if (pool.enemy_name == "M2_CommandCenter")
            {
                @centers = @pool;
                continue;
            }

            if (pool.enemy_name == "M2_PowerPlant")
            {
                @powerPlants = @pool;
                continue;
            }

            if (pool.enemy_name == "M2_Barracks")
            {
                @barracks = @pool;
                continue;
            }
        }

        commanders.resize(commandersSpawn.get_length());

        ///curObjective = 2;
    }

    bool IsCompleted()
    {
        return curObjective == 9;
    }

    void UpdateSoldier(float dt, Soldier@ soldier, int building_hp, Matrix spawnPoint)
    {
        if (!soldier.spawned)
        {
            if (building_hp <= 0)
            {
                soldier.spawned = true;
                soldier.visible = 1;
                soldier.phys.SetTransform(spawnPoint);
            }
            else
            {
                ShowMarker(spawnPoint.GetPos(), 0);
            }
        }

        auto mat = soldier.mesh.GetTransform();
        auto pos = mat.GetPos();

        if (soldier.spawned)
        {
            if (soldier.grab_state == 0)
            {
                if (soldier.time_to_move < 2.0f)
                {
                    soldier.time_to_move += dt;

                    pos.z -= dt * 12.0f;

                    mat.SetPos(pos);
                    soldier.phys.SetTransform(mat);
                }

                if (main.TryGrabSoldier(pos, dt))
                {
                    soldier.grab_state = 1;
                    main.StartGrabbing();
                }
            }
            else
            if (soldier.grab_state == 1)
            {
                if (!main.IsGrabbing())
                {
                    soldier.visible = 0;
                    soldier.grab_state = 2;
                }
                else
                {
                    pos = main.heli_pos;
                    pos.y -= main.ledge_pos;

                    mat.SetPos(pos);
                    soldier.phys.SetTransform(mat);
                }
            }
        }
    }

    void Update(float dt) override
    {
        if (curObjective == 0)
        {
            ShowObjective("M2: Destroy Command centers & capture commanders");

            int grabbed = 0;

            for (int i = 0; i < commanders.get_length(); i++)
            {
                auto@ commander = commanders[i];

                UpdateSoldier(dt, commander, centers.enemies[i].hp, commandersSpawn[i].transform);

                if (commander.grab_state == 2)
                {
                    grabbed++;
                }
            }

            if (grabbed == commanders.get_length())
            {
                curObjective++;
            }
        }
        else
        if (curObjective == 1)
        {
            ShowObjective("M2: Destroy power plant");

            if (CheckEnemies(powerPlants) == 0)
            {
                curObjective++;
            }
        }
        else
        if (curObjective == 2)
        {
            ShowObjective("M2: Destroy barracks & retrive spy");

            UpdateSoldier(dt, spy, barracks.enemies[0].hp, spySpawn.transform);

            if (spy.grab_state == 2)
            {
                curObjective++;
            }
        }
    }
}